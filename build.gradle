buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
	mavenCentral()
	maven {
		name 'slimevoid'
		url 'http://hyda.slimevoid.net/maven/repos'
	}
}

apply plugin: 'maven'
apply plugin: 'forge'
apply plugin: 'eclipse'
def slimevoidVersion = '3.0.0.4-beta-1.8'
def number = '2.2.5.1'
def release = 'alpha'
def mcversion = '1.8'
def forge = '11.14.1.1334'
def map = 'snapshot_20150302'
def mc = mcversion + '-' + forge
def pack = 'littleblocks'
def github = 'LittleBlocks-FML'
version = number + '-' + release + '-' + mcversion
group = 'net.slimevoid'
archivesBaseName = 'LittleBlocks'

dependencies {
    if (findProject(':SlimevoidLibrary') != null ) {
        compile project(':SlimevoidLibrary')
    } else {
        compile 'net.slimevoid:SlimevoidLibrary:' + slimevoidVersion + ':dev'
    }
}

minecraft {
    version = mc
    mappings = map
    if  (file('../run').exists()) {
        runDir = "../run"
    } else {
        runDir = "run"
    }
    replaceIn 'net/slimevoid/' + pack + '/core/lib/CoreLib.java'
    replace '@VERSION@', project.version
    replace 'required-after:SlimevoidLib', 'required-after:Forge@[' + forge + ',);required-after:SlimevoidLib@[' + slimevoidVersion + ',)'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'dev'
}

build.dependsOn sourceJar,deobfJar

configurations {
	deployJars
}

dependencies {
    deployJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

artifacts {
	archives sourceJar
	archives deobfJar
}

uploadArchives {
	dependsOn 'reobf'
    repositories.mavenDeployer {
    	configuration = configurations.deployJars
        if (project.hasProperty("slimevoidUsername")) {
            repository(url: 'scp://hyda.slimevoid.net/home/maven/www/repos') {
                authentication(userName: slimevoidUsername, password: slimevoidPassword)
            }
        } else {
            repository(url: 'file://localhost/' + project.file('build/repo').getAbsolutePath())
        }
        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description archivesBaseName
            url 'https://github.com/Slimevoid/' + github

            scm {
                url 'https://github.com/Slimevoid/' + github
                connection 'scm:git:git://github.com/Slimevoid/' + github + '.git'
                developerConnection 'scm:git:git@Slimevoid/' + github + '.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/Slimevoid/' + github + '/issues'
            }

            licenses {
                license {
                    name 'Lesser General Public Licence'
                    url 'https://raw.github.com/Slimevoid/' + github + '/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Eurymachus'
                    name 'Eurymachus'
                    roles { role 'developer' }
                }
                developer {
                    id 'Slimevoid'
                    name 'Slimevoid Dev Team'
                    roles { role 'developer' }
                }
            }
        }
    }
}
